# Ataque STAB mas fuerte

PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?name ?powerStat ?type ?moves ?category ?effectivePower WHERE {
 ?pokemon rdf:type ex:Pokemon ; ex:forme ?name ; ex:type1 ?type ; ex:attack ?atk ; ex:spattack ?spatk.
 ?moveset rdf:type ex:Moveset ; ex:forPokemon ?pokemon ; ex:moves ?moves .
 ?moves ex:type ?type ; ex:category ?category ;ex:power ?movePower .

 BIND(xsd:int(?movePower) AS ?movePowerInt)
  BIND(
    IF(?category = "Physical", ?atk, ?spatk) AS ?powerStat 
  )
  
    BIND(
    FLOOR(22 * ((?movePowerInt * ?powerStat)/83) * 1.5 ) as ?effectivePower
  )
  
  FILTER(?effectivePower > 0)
} order by desc(?effectivePower)

# Fastest support pokemon



PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?name ?speed (GROUP_CONCAT(?moveName; separator=", ") AS ?allMoves) WHERE {
 ?pokemon rdf:type ex:Pokemon ; ex:forme ?name ; ex:speed ?speed .
  ?moveset rdf:type ex:Moveset ; ex:forPokemon ?pokemon ; ex:moves ?moves .
  ?moves ex:category "Status" ; ex:name ?moveName .
} group by ?name ?speed 
order by desc(?speed)



# Hazards removal pokemon

PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?pokemonName ?type1 ?type2 ?moveName WHERE {
  # Relación entre Pokémon y sus movimientos
  ?pokemon rdf:type ex:Pokemon ; ex:forme ?pokemonName ; ex:type1 ?type1 ; ex:type2 ?type2 .
  ?moveset rdf:type ex:Moveset ; ex:forPokemon ?pokemon ; ex:moves ?move .

  # Filtrar movimientos de remoción de hazards
  ?move ex:name ?moveName .
  FILTER(?moveName IN ("Defog", "Rapid Spin", "Court Change", "Mortal Spin"))
}
ORDER BY ?pokemonName


#Pokemon con la mayor cobertura de tipos
PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?pokemonName (COUNT(DISTINCT ?moveType) AS ?typeCoverage) WHERE {
  ?pokemon rdf:type ex:Pokemon ; ex:forme ?pokemonName .
  ?moveset rdf:type ex:Moveset ; ex:forPokemon ?pokemon ; ex:moves ?move .
  ?move ex:type ?moveType .
}
GROUP BY ?pokemonName
ORDER BY DESC(?typeCoverage)
LIMIT 10





#Tankiest pokemon

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX ex: <http://example.org/>

SELECT ?pokemonName ?hp ?defense ?spDefense ?totalDefense WHERE {
  ?pokemon rdf:type ex:Pokemon ; ex:forme ?pokemonName ; ex:hp ?hp ; ex:defense ?defense ; ex:spdefense ?spDefense .

  BIND(?hp + ?defense + ?spDefense AS ?totalDefense)
}
ORDER BY DESC(?totalDefense)
LIMIT 10


#Pokemon with the most moves
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX ex: <http://example.org/>

SELECT ?pokemonName (COUNT(?move) AS ?moveCount) WHERE {
  ?pokemon rdf:type ex:Pokemon ; ex:forme ?pokemonName .
  ?moveset rdf:type ex:Moveset ; ex:forPokemon ?pokemon ; ex:moves ?move .
}
GROUP BY ?pokemonName
ORDER BY DESC(?moveCount)
LIMIT 10


#Pokémon with Priority Moves
PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?pokemonName ?moveName ?priorityInt WHERE {
  ?pokemon rdf:type ex:Pokemon ; ex:forme ?pokemonName .
  ?moveset rdf:type ex:Moveset ; ex:forPokemon ?pokemon ; ex:moves ?move .
  ?move ex:name ?moveName ; ex:priority ?priority .
  BIND(xsd:integer(?priority) AS ?priorityInt)
}
ORDER BY DESC (?priorityInt) ?pokemonName



# Faster Pokémon

PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?name ?speed (GROUP_CONCAT(?moveName; separator=", ") AS ?allMoves) WHERE {
  ?specificPokemon rdf:type ex:Pokemon ;
                   ex:forme "Pikachu" ;
                   ex:speed ?specificSpeed .
  ?pokemon rdf:type ex:Pokemon ;
           ex:forme ?name ;
           ex:speed ?speed .
  FILTER(?speed > ?specificSpeed)
}
GROUP BY ?name ?speed
ORDER BY DESC(?speed)


# Abilities by Move Usage

PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?pokemonName WHERE {
  ?pokemon rdf:type ex:Pokemon ;
           ex:forme ?pokemonName ;
           ex:ability1 ?ability ;
           ex:ability2 ?ability2 ;
           ex:abilityH ?abilityH .

  FILTER(?ability = "Sturdy" || ?ability2 = "Sturdy" || ?abilityH = "Sturdy")

  ?moveset rdf:type ex:Moveset ;
           ex:forPokemon ?pokemon ;
           ex:moves ?move .

  ?move ex:name "Self-Destruct" .
}


# Compatibility of Pokémon and Moves

PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?pokemonName ?moveName WHERE {
  ?pokemon rdf:type ex:Pokemon ;
           ex:forme ?pokemonName ;
           ex:type1 ?type1 ;
           ex:type2 ?type2 .
  FILTER(?type1 = "Fire" || ?type2 = "Fire")

  ?moveset rdf:type ex:Moveset ;
           ex:forPokemon ?pokemon ;
           ex:moves ?move .

  ?move ex:name ?moveName ;
        ex:type "Grass" .
}
ORDER BY ?pokemonName ?moveName


# Find Pokémon by Move

PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?pokemonName WHERE {
  ?move ex:name "Trick Room" .

  ?moveset rdf:type ex:Moveset ;
           ex:moves ?move ;
           ex:forPokemon ?pokemon .

  ?pokemon rdf:type ex:Pokemon ;
           ex:forme ?pokemonName .
}
ORDER BY ?pokemonName


# First Partner Pokémon

PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?pokemonName ?ndex WHERE {
  ?pokemon rdf:type ex:Pokemon ;
           ex:forme ?pokemonName ;
           ex:ability1 ?ability1 ;
           ex:ndex ?ndex .
  FILTER(?ability1 = "Overgrow" || ?ability1 = "Blaze" || ?ability1 = "Torrent")
}
ORDER BY ?ndex
